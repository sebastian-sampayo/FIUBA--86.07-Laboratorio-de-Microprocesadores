PUBLIC MOVIMIENTO, INICIALIZAR_CTRL_MOUSE

EXTRN DATA(ACELERACION_POSITIVA_X, ACELERACION_POSITIVA_Y)
EXTRN DATA(ACELERACION_NEGATIVA_X, ACELERACION_NEGATIVA_Y)

;constantes
MAX_ACELERACION EQU 25

;pines
SALIDAX EQU P3.4
SALIDAY EQU P3.6
PSYNCX EQU P3.5
PSYNCY EQU P3.7

CTRL_MOUSE_CODE SEGMENT CODE
RSEG CTRL_MOUSE_CODE



;Pongo los pines de salida en cero, para no perturbar la señal patrón
INICIALIZAR_CTRL_MOUSE:
	CLR SALIDAX
	CLR SALIDAY
	RET


MOVIMIENTO:	


;Me fijo a ver si hay aceleración hacia algun lado
DER:	MOV A, ACELERACION_NEGATIVA_X
		JNZ DERECHA
		
IZQ:	MOV A, ACELERACION_POSITIVA_X
		JNZ IZQUIERDA			
		
ARR:	MOV A,  ACELERACION_NEGATIVA_Y
		JNZ ARRIBA
		
ABA:	MOV A, ACELERACION_POSITIVA_Y
		JNZ ABAJO
		
			
FIN:	RET


;Si hay aceleracion, entro en alguna de estas funciones.
;Se mandan las distintas señales activas tantas veces como el valor que 
;haya obtenido en ADEC_ACELERACION

DERECHA:
		MOV R1,ACELERACION_NEGATIVA_X
		MOV A,#MAX_ACELERACION

		;Sicronizo: me posiciono en un punto conocido del periodo
		;de la señal patron, 
		;para saber cuando mandar las señales activas
		ACALL SYNCX					
		

LOOPDER:
		NOP
		NOP
		NOP
		
		SETB SALIDAX			;Envio la señal activa (pulso de 110us)
		ACALL D110
		CLR SALIDAX
		
		NOP
		NOP 					;
		
		JNB PSYNCX, $			
		DJNZ R1, LOOPDER
		CLR C
		SUBB A, ACELERACION_NEGATIVA_X
		JMP FINALX
		
IZQUIERDA:
		MOV R1,ACELERACION_POSITIVA_X
		MOV A,#MAX_ACELERACION
		
		ACALL SYNCX

LOOPIZQ:		
		JNB PSYNCX, $
		
		ACALL D6
		
		SETB SALIDAX
		ACALL D10
		CLR SALIDAX
		
		DJNZ R1, LOOPIZQ
		
		CLR C
		SUBB A, ACELERACION_POSITIVA_X
		JMP FINALX

ARRIBA:

		MOV R1, ACELERACION_NEGATIVA_Y
		MOV A,#MAX_ACELERACION
		

		ACALL SYNCY
		
LOOPARR:
		JNB PSYNCY,$
		
		ACALL D6
		
		SETB SALIDAY
		ACALL D10
		CLR SALIDAY
			
		DJNZ R1, LOOPARR
		
		CLR C
		SUBB A, ACELERACION_NEGATIVA_Y
		JMP FINALY
		
ABAJO:
		MOV R1, ACELERACION_POSITIVA_Y
		MOV A, #MAX_ACELERACION

LOOPABA:
		ACALL SYNCY
		
		NOP
		NOP
		NOP
		
		SETB SALIDAY
		ACALL D110
		CLR SALIDAY
		
		
		DJNZ R1, LOOPABA
		
		CLR C
		SUBB A,ACELERACION_POSITIVA_Y
		JMP FINALY

FINALX:
		
FX:		ACALL SYNCX
		DJNZ ACC,FX

		JMP ARR
FINALY:	
FY:		ACALL SYNCY
		DJNZ ACC, FY
		
		JMP FIN
SYNCX:	
		JNB PSYNCX, SYNCX
		ACALL D18
		JNB PSYNCX, SYNCX
		RET
		
SYNCY:	
		JNB PSYNCY, SYNCY
		ACALL D18
		JNB PSYNCY, SYNCY
		RET
		
D18: 	
		MOV R5,#6
		NOP
		DJNZ R5,$
		RET
		
D6:		
		NOP
		NOP
		RET
		
D10:	
		ACALL D6
		RET
		
D110:	
		MOV R5,#52 ;65
		DJNZ R5,$
		NOP
		RET

		END
